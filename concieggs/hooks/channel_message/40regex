#!/usr/bin/env tclsh
#
# Reagér på 's/foo/bar/[ig]' og 'person: s/foo/bar/[ig]'.

set user $::env(EGGS_USER)
set body $::env(EGGS_BODY)
set db $::env(CONCIEGGS_DB_DIR)

# Kør et program og returnér om det lykkes (exit-kode 0 = succes).
proc success args {
    catch [list exec {*}$args] output options
    return [expr {![dict get $options -code]}]
}

# Kør et program og returnér outputtet som en liste af linjer.
proc run args {
    catch [list exec {*}$args] output options
    return [split $output "\n"]
}

proc mainAlias {alias} {
    lindex [run mainAlias $alias] 0
}

set s// {^(?:([^ ,:]+)[,:] *)?s/((?:[^\\/]|\\[\\/])*)/((?:[^\\/]|\\[\\/])*)(?:/([ig]*))?}
if {![regexp ${s//} $body {} who pattern replacement modifiers]} {exit 0}
if {![string length $who]} {set who $user}

# Med 's/foo/bar/'-syntaksen er det svært at henvise til en bogstavelig '/'
# da de også bliver brugt som separator mellem mønstre. I Perl regex kan man
# skrive '\/' for at få en bogstavelig skråstreg. Det kan man ikke med TCL's
# regex, så efter komponenterne af 's/foo/bar/' er blevet udvundet, erstattes
# '\/' med almindelige '/'er.
foreach var {pattern replacement} {regsub -all {\\/} [set $var] "/" $var}

set lastmsgs [run lastmsgs $who]
if {![llength $lastmsgs]} {
    set thou [expr {$user == $who ? "du" : $who}]
    puts "$user: Men $thou har jo ikke sagt noget!"
    exit
}

set flags {}
if {[string first i $modifiers] != -1} {lappend flags -nocase}
if {[string first g $modifiers] != -1} {lappend flags -all}

for {set i [expr {[llength $lastmsgs] - 1}]} {$i >= 0} {incr i -1} {
    set lastmsg [lindex $lastmsgs $i]
    try {
        set did_replace [regsub {*}$flags -- $pattern $lastmsg $replacement lastmsg]
    } on error {errmsg options} {
        puts "$user: Det udtryk har jeg svært ved at greje.  Jeg hører følgende i min øresnegl: $errmsg."
        run annoyedBy $user 1 "brugte et regulært udtryk som ikke kunne oversættes"
        exit
    }

    if {$did_replace} {
        if {[regexp {\r|\n} $lastmsg]} {
            puts "$user: Hov, hov.  Hold dine rettelser på én linje, tak!"
            exit
        }

        if {[regexp {^\001ACTION (.*?)\001$} $lastmsg {} lastaction]} {
            puts "Rettet: * $who $lastaction"
        } else {
            puts "Rettet: <$who> $lastmsg"
        }

        if {$user == $who} {
            lset lastmsgs $i $lastmsg
            set fp [open "$db/store/users/[mainAlias $who]/lastmsg" w]
            puts $fp [join $lastmsgs "\n"]
            close $fp
        }

        exit
    }
}
