#!/usr/bin/env perl
use 5.020;
use strict;
use warnings;

use IO::All;
use File::Basename;

my %interps = (
    'sh'      => 'Bourne Shell',
    'bash'    => 'BASH',
    'bf'      => 'Brainfuck',
    'dash'    => 'DASH',
    'fast'    => 'Gotta Go Fast!',
    'node'    => 'JavaScript',
    'perl'    => 'Perl 5',
    'perl6'   => 'Perl 6',
    'php'     => 'PHP',
    'python'  => 'Python 2',
    'python3' => 'Python 3',
    'rash'    => 'RASH',
    'ruby'    => 'Ruby',
    'runghc'  => 'Haskell',
    'tclsh8.6'=> 'TCL',
    'clisp'   => 'Common Lisp',
);

my %exts = (
    'c'    => 'C',
    'go'   => 'Go',
    'hs'   => 'Haskell',
    'kex'  => 'Kleenex',
    'myr'  => 'Myrddin',
    'pas'  => 'Pascal',
    'sml'  => 'Standard ML',
    'ml'   => 'OCaml',
    'rust' => 'Rust',
    'cpp'  => 'C++',
);

my %execs = (
    'emacs' => 'Emacs Lisp',
    'sbcl'  => 'Common Lisp',
);

my $filename = shift @ARGV;
exit unless $filename;
my $io = io($filename);
my ($interp) = ($io->getline // '') =~ /^#!.*?([^\/\s]+)$/;
$interp //= '';

if ($interp eq 'sh') {
    if (my ($exec) = ($io->getline // '') =~ /exec ([^ ]+)/) {
        if (my $guess = $execs{$exec}) {
            print $guess;
            exit(0);
        }
    }
}

if ($interp && (my $guess = $interps{$interp})) {
    print $guess;
    exit(0);
}

my $filename_noext = basename($filename);
if (-d "compiled/$filename_noext.make") {
    print "Flertydig (Makefile)";
    exit(0);
}

my @file_compiled = grep { $_ ne '.' && $_ ne '..' } glob("compiled/$filename_noext.*");
my @exts = map { (split /\./, $_)[-1] } @file_compiled;
if (@file_compiled > 1) {
    print "Flertydig (" . (join ', ', @exts) . ")";
    exit(1);
}

if (@exts > 0 && (my $guess = $exts{$exts[0]})) {
    print $guess;
    exit(0);
}

print "Ukendt";
exit(1);
